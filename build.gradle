import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'fabric-loom' version '1.11.8'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm' version '2.2.20'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    accessWidenerPath = file("src/main/resources/vminus.accesswidener")

    mods {
        "vminus" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    interfaceInjection {
        enableDependencyInterfaceInjection = true
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    flatDir {
        dirs 'libs'
    }

    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"


    modImplementation("net.fabricmc:fabric-language-kotlin:1.13.6+kotlin.2.2.20")
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-world:${project.cca_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-block:${project.cca_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-item:${project.cca_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-level:${project.cca_version}"

    include "org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-world:${project.cca_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-block:${project.cca_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-item:${project.cca_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-level:${project.cca_version}"
    include "net.fabricmc:fabric-language-kotlin:1.13.6+kotlin.2.2.20"

    implementation "io.github.jaredmdobson:concentus:1.0.2"
    implementation "org.gagravarr:vorbis-java-core:0.8"

    shadow "io.github.jaredmdobson:concentus:1.0.2"
    shadow "org.gagravarr:vorbis-java-core:0.8"
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate "io.github.jaredmdobson.concentus", "net.lixir.vminus.shaded.concentus"
    relocate "org.gagravarr", "net.lixir.vminus.shaded.vorbis"

    from(sourceSets.main.output) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude 'META-INF/**'
    }

    zip64 = true
}


processResources {
    var expandProps = [
            minecraft_version            : minecraft_version,
            minecraft_version_range      : minecraft_version_range,
            mod_id                       : mod_id,
            mod_name                     : mod_name,
            mod_description              : mod_description,
            mod_version                  : mod_version,
            mod_authors                  : mod_authors,
            mod_license                  : mod_license,
            maven_group                  : maven_group,
            loader_version          : loader_version
    ]
    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', "*.mixins.json"]) {
        expand expandProps
    }
    inputs.properties(expandProps)
}
def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "21"
        freeCompilerArgs += ["-Xjvm-default=all"]
    }
}



tasks.remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile.get().asFile
    archiveClassifier.set("")
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion)
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            version = project.version
            group = project.group
            from components.java


        }
    }

    repositories {
        maven {
            name = "localModRepo"
            url = uri("${rootDir}/../local-maven")
        }
    }
}
